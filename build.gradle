plugins {
    id 'fabric-loom' version '0.10-SNAPSHOT'
    id("io.github.juuxel.loom-quiltflower-mini") version "1.1.0"
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

archivesBaseName = "${project.mod_id}-fabric-${minecraft_version}"
version = (hasProperty("CIRevision") ? CIRevision : project.mod_version)
group = project.group_name

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

repositories {
    maven { url = "https://cursemaven.com" }
    maven { url = "https://maven.shedaniel.me/" }
    maven { url = uri("https://storage.googleapis.com/devan-maven/") }
    maven { url = "https://maven.terraformersmc.com/releases/" }
    maven { url "https://nexus.resourcefulbees.com/repository/maven-public/" }
    maven { url 'https://jitpack.io' }
    maven {
        name = 'Ladysnake Mods'
        url = 'https://ladysnake.jfrog.io/artifactory/mods'
    }
    maven {
        url 'https://repo.maven.apache.org/maven2'
        name 'Maven Central'
    }
    maven {
        name = "BuildCraft"
        url = "https://mod-buildcraft.com/maven"
    }
    maven {
        name = "ParchmentMC"
        url = "https://maven.parchmentmc.net/"
    }
    maven {
        url = "https://maven.jamieswhiteshirt.com/libs-release"
        content {
            includeGroup("com.jamieswhiteshirt")
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"

    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.17.1:2021.10.31@zip")
    }

    //Dev Environment Only
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:${project.rei_version}"
    modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"

    implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    //modImplementation "com.telepathicgrunt:Blame-Fabric:1.17-4.2.0-fabric"
    modImplementation 'com.github.Chocohead:Fabric-ASM:v2.3'

    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modApi "com.terraformersmc:modmenu:2.0.2"
    modApi("me.shedaniel.cloth:cloth-config-fabric:5.0.34") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    //Compiled Jar Side
    modApi "me.shedaniel.cloth.api:cloth-api:${project.cloth_api_version}"
    include "me.shedaniel.cloth.api:cloth-api:${project.cloth_api_version}"

    modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:3.0.0"
    modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:3.0.0"
    include "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:3.0.0"
    include "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:3.0.0"

    include 'com.github.Chocohead:Fabric-ASM:v2.3'
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"

    it.options.release = 17
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}